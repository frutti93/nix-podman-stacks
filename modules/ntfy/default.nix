{
  config,
  lib,
  options,
  ...
}: let
  name = "ntfy";
  storage = "${config.tarow.podman.storageBaseDir}/${name}";
  cfg = config.tarow.podman.stacks.${name};
in {
  imports = import ../mkAliases.nix config lib name [name];

  options.tarow.podman.stacks.${name} = {
    enable = lib.mkEnableOption name;
    enableGrafanaDashboard = lib.mkEnableOption "Grafana Dashboard";
    enablePrometheusExport = lib.mkEnableOption "Prometheus Export";
    env = lib.mkOption {
      type = (options.services.podman.containers.type.getSubOptions []).environment.type;
      default = {};
      description = "Additional environment variables passed to the container";
    };
    envFile = lib.mkOption {
      type = lib.types.nullOr lib.types.path;
      default = null;
      description = ''
        Environment file passed to the container. Can be used to pass secrets such as Webpush Keys.
        In order to enable web push support, make sure the env file contains 'NTFY_WEB_PUSH_PUBLIC_KEY',
        'NTFY_WEB_PUSH_PRIVATE_KEY' & 'NTFY_WEB_PUSH_EMAIL_ADDRESS' variables.
        Keys can be generated by running `podman run --rm docker.io/binwiederhier/ntfy:latest webpush keys`
      '';
    };
  };

  config = lib.mkIf cfg.enable {
    tarow.podman.stacks.monitoring = {
      grafana.dashboards = lib.optional cfg.enableGrafanaDashboard ./grafana_dashboard.json;
      prometheus.config.scrape_configs = lib.optional cfg.enablePrometheusExport {
        job_name = "ntfy";
        honor_timestamps = true;
        metrics_path = "/metrics";
        scheme = "http";
        static_configs = [{targets = ["${name}:80"];}];
      };
    };

    services.podman.containers.${name} = {
      image = "docker.io/binwiederhier/ntfy:v2.13.0";
      exec = "serve";
      volumes = [
        "${storage}:/var/lib/ntfy"
      ];
      environment =
        {
          NTFY_BASE_URL = cfg.containers.${name}.traefik.serviceDomain;
          NTFY_CACHE_FILE = "/var/lib/ntfy/cache.db";
          NTFY_AUTH_FILE = "/var/lib/ntfy/auth.db";
          NTFY_AUTH_DEFAULT_ACCESS = "deny-all";
          NTFY_BEHIND_PROXY = true;
          NTFY_ATTACHMENT_CACHE_DIR = "/var/lib/ntfy/attachments";
          NTFY_ENABLE_LOGIN = true;
          NTFY_UPSTREAM_BASE_URL = "https://ntfy.sh";
          NTFY_WEB_PUSH_FILE = "/var/lib/ntfy/webpush.db";
          NTFY_ENABLE_METRICS = cfg.enablePrometheusExport;
        }
        // cfg.env;
      environmentFile = lib.optional (cfg.envFile != null) cfg.envFile;

      port = 80;
      traefik.name = name;
      homepage = {
        category = "General";
        name = "ntfy";
        settings = {
          description = "Push Notifications";
          icon = "ntfy";
        };
      };
    };
  };
}
