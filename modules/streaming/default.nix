{
  config,
  lib,
  pkgs,
  ...
}: let
  stackName = "streaming";

  jellyfinName = "jellyfin";
  sonarrName = "sonarr";
  radarrName = "radarr";
  bazarrName = "bazarr";
  seerrName = "seerr";
  profilarrName = "profilarr";
  profilarrParserName = "${profilarrName}-parser";
  maintainerrName = "maintainerr";

  category = "Media & Downloads";
  jellyfinDescription = "Media Server";
  jellyfinDisplayName = "Jellyfin";
  sonarrDescription = "Series Management";
  sonarrDisplayName = "Sonarr";
  radarrDescription = "Movie Management";
  radarrDisplayName = "Radarr";
  bazarrDescription = "Subtitle Management";
  bazarrDisplayName = "Bazarr";
  seerrDescription = "Media Requests";
  seerrDisplayName = "Seerr";
  profilarrDisplayName = "Profilarr";
  profilarrDescription = "Configuration Management";

  maintainerrDisplayName = "Maintainerr";
  maintainerrDescription = "Library Maintenance";

  cfg = config.nps.stacks.${stackName};
  storage = "${config.nps.storageBaseDir}/${stackName}";
  mediaStorage = "${config.nps.mediaStorageBaseDir}";

  arrlib = import ../arrlib.nix {
    inherit
      config
      lib
      pkgs
      stackName
      storage
      mediaStorage
      category
      ;
  };
in {
  imports = import ../mkAliases.nix config lib stackName [
    jellyfinName
    sonarrName
    radarrName
    bazarrName
    seerrName
    profilarrName
    profilarrParserName
    maintainerrName
  ];

  options.nps.stacks.${stackName} =
    {
      enable = lib.mkEnableOption stackName;
      useQbittorrent = lib.mkOption {
        type = lib.types.bool;
        default = true;
        description = ''
          Whether to enable the qBittorrent stack and connect it to the streaming network.
          The qBittorrent stack (Gluetun, qBittorrent and qui) runs standalone and is attached to the streaming network and Traefik.
        '';
      };
      useProwlarr = lib.mkOption {
        type = lib.types.bool;
        default = true;
        description = ''
          Whether to enable the Prowlarr stack and connect it to the streaming network.
          The Prowlarr stack runs standalone and is attached to the streaming network and Traefik.
        '';
      };
      useSabnzbd = lib.mkOption {
        type = lib.types.bool;
        default = false;
        description = ''
          Whether to enable the SABnzbd stack and connect it to the streaming network.
          The SABnzbd stack runs standalone and is attached to the streaming network and Traefik.
        '';
      };
      jellyfin = {
        enable =
          lib.mkEnableOption "Jellyfin"
          // {
            default = true;
          };
        oidc = {
          enable = lib.mkOption {
            type = lib.types.bool;
            default = false;
            description = ''
              Whether to enable OIDC login with Authelia. This will register an OIDC client in Authelia
              and setup the necessary configuration file.

              The plugin configuration will be automatically provided, the plugin itself has to be installed in the
              Jellyfin Web-UI tho.

              For details, see:

              - <https://www.authelia.com/integration/openid-connect/clients/jellyfin/>
              - <https://github.com/9p4/jellyfin-plugin-sso>
            '';
          };
          clientSecretFile = (import ../authelia/options.nix lib).clientSecretFile;
          clientSecretHash = (import ../authelia/options.nix lib).derivableClientSecretHash cfg.jellyfin.oidc.clientSecretFile;
          adminGroup = lib.mkOption {
            type = lib.types.str;
            default = "${jellyfinName}_admin";
            description = "Users of this group will be assigned admin rights in Jellyfin";
          };
          userGroup = lib.mkOption {
            type = lib.types.str;
            default = "${jellyfinName}_user";
            description = "Users of this group will be able to log in";
          };
        };
      };
      profilarr = {
        enable = lib.mkEnableOption "Profilarr";
        enableParser = lib.mkEnableOption "Profilarr Parser";
        oidc = {
          enable = lib.mkOption {
            type = lib.types.bool;
            default = false;
            description = ''
              Whether to enable OIDC login with Authelia. This will register an OIDC client in Authelia
              and setup the necessary configuration.

              For details, see:

              - <https://v2.dictionarry.dev/profilarr-setup/installation?section=authentication>
            '';
          };
          clientSecretFile = (import ../authelia/options.nix lib).clientSecretFile;
          clientSecretHash = (import ../authelia/options.nix lib).derivableClientSecretHash cfg.profilarr.oidc.clientSecretFile;
          userGroup = lib.mkOption {
            type = lib.types.str;
            default = "${profilarrName}_user";
            description = "Users of this group will be able to log in";
          };
        };
      };
      seerr.enable = lib.mkEnableOption "Seerr";
      maintainerr.enable = lib.mkEnableOption "Maintainerr";
    }
    // (
      lib.genAttrs
      [
        sonarrName
        radarrName
        bazarrName
      ]
      arrlib.mkArrOptions
    );

  config = lib.mkIf cfg.enable {
    # Enable qBittorrent stack. Attach Gluetun to the streaming network when the VPN is enabled, otherwise qBittorrent directly
    nps.stacks.qbittorrent.enable = lib.mkIf cfg.useQbittorrent true;
    # Enable Prowlarr stack & connect it to the streaming stack network
    nps.stacks.prowlarr.enable = lib.mkIf cfg.useProwlarr true;
    # Enable SABnzbd stack & connect it to the streaming stack network
    nps.stacks.sabnzbd.enable = lib.mkIf cfg.useSabnzbd true;
    nps.containers = lib.mkMerge [
      (lib.mkIf cfg.useQbittorrent {
        ${
          if config.nps.stacks.qbittorrent.gluetun.enable
          then "gluetun"
          else "qbittorrent"
        }.network = [stackName];
      })
      (lib.mkIf cfg.useProwlarr {
        prowlarr.network = [stackName];
      })
      (lib.mkIf cfg.useSabnzbd {
        sabnzbd.network = [stackName];
      })
    ];

    nps.stacks.lldap.bootstrap.groups = lib.mkMerge [
      (lib.mkIf (cfg.jellyfin.enable && cfg.jellyfin.oidc.enable) {
        ${cfg.jellyfin.oidc.adminGroup} = {};
        ${cfg.jellyfin.oidc.userGroup} = {};
      })
      (lib.mkIf (cfg.profilarr.enable && cfg.profilarr.oidc.enable) {
        ${cfg.profilarr.oidc.userGroup} = {};
      })
    ];
    nps.stacks.authelia = lib.mkMerge [
      (lib.mkIf (cfg.jellyfin.enable && cfg.jellyfin.oidc.enable) {
        oidc.clients.${jellyfinName} = {
          client_name = "Jellyfin";
          client_secret = cfg.jellyfin.oidc.clientSecretHash;
          public = false;
          authorization_policy = config.nps.stacks.authelia.defaultAllowPolicy;
          require_pkce = true;
          pkce_challenge_method = "S256";
          pre_configured_consent_duration = config.nps.stacks.authelia.oidc.defaultConsentDuration;
          token_endpoint_auth_method = "client_secret_post";
          redirect_uris = [
            "${cfg.containers.${jellyfinName}.traefik.serviceUrl}/sso/OID/redirect/authelia"
          ];
        };
      })
      (lib.mkIf (cfg.profilarr.enable && cfg.profilarr.oidc.enable) {
        oidc.clients.${profilarrName} = {
          client_name = profilarrDisplayName;
          client_secret = cfg.profilarr.oidc.clientSecretHash;
          public = false;
          authorization_policy = profilarrName;
          require_pkce = false;
          pkce_challenge_method = "";
          pre_configured_consent_duration = config.nps.stacks.authelia.oidc.defaultConsentDuration;
          token_endpoint_auth_method = "client_secret_post";
          redirect_uris = [
            "${cfg.containers.${profilarrName}.traefik.serviceUrl}/auth/oidc/callback"
          ];
        };
        # No real RBAC control based on custom claims / groups yet. Restrict user-access on Authelia level
        settings.identity_providers.oidc.authorization_policies.${profilarrName} = {
          default_policy = "deny";
          rules = [
            {
              policy = config.nps.stacks.authelia.defaultAllowPolicy;
              subject = "group:${cfg.profilarr.oidc.userGroup}";
            }
          ];
        };
      })
    ];

    services.podman.containers =
      {
        ${jellyfinName} = let
          brandingXml = pkgs.writeText "branding.xml" ''
            <?xml version="1.0" encoding="utf-8"?>
            <BrandingOptions xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns:xsd="http://www.w3.org/2001/XMLSchema">
              <LoginDisclaimer>&lt;form action="${config.nps.containers.jellyfin.traefik.serviceUrl}/sso/OID/start/authelia"&gt;
              &lt;button class="raised block emby-button button-submit"&gt;
                Sign in with Authelia
              &lt;/button&gt;
            &lt;/form&gt;</LoginDisclaimer>
              <CustomCss>a.raised.emby-button {
              padding: 0.9em 1em;
              color: inherit !important;
            }
            .disclaimerContainer {
              display: block;
            }</CustomCss>
              <SplashscreenEnabled>true</SplashscreenEnabled>
            </BrandingOptions>
          '';
        in
          lib.mkIf cfg.jellyfin.enable {
            image = "lscr.io/linuxserver/jellyfin:10.11.11";
            volumeMap = {
              config = "${storage}/${jellyfinName}:/config";
              media = "${mediaStorage}:/media";
              brandingXml = lib.mkIf (cfg.jellyfin.oidc.enable) "${brandingXml}:/config/branding.xml";
            };

            templateMount = lib.optional cfg.jellyfin.oidc.enable {
              templatePath = pkgs.writeText "oidc-template" (
                import ./jellyfin_sso_config.nix {
                  autheliaUri = config.nps.containers.authelia.traefik.serviceUrl;
                  clientId = jellyfinName;
                  adminGroup = cfg.jellyfin.oidc.adminGroup;
                  userGroup = cfg.jellyfin.oidc.userGroup;
                  clientSecretFile = cfg.jellyfin.oidc.clientSecretFile;
                }
              );
              destPath = "/config/data/plugins/configurations/SSO-Auth.xml";
            };

            devices = ["/dev/dri:/dev/dri"];
            environment = {
              PUID = config.nps.defaultUid;
              PGID = config.nps.defaultGid;
              JELLYFIN_PublishedServerUrl =
                config.services.podman.containers.${jellyfinName}.traefik.serviceUrl;
            };

            port = 8096;
            stack = stackName;
            traefik.name = jellyfinName;
            homepage = {
              inherit category;
              name = jellyfinDisplayName;
              settings = {
                description = jellyfinDescription;
                icon = "jellyfin";
                widget.type = "jellyfin";
              };
            };
            glance = {
              inherit category;
              description = jellyfinDescription;
              name = jellyfinDisplayName;
              id = jellyfinName;
              icon = "di:jellyfin";
            };
          };

        ${seerrName} = lib.mkIf cfg.seerr.enable {
          image = "ghcr.io/seerr-team/seerr:v3.4.1";
          user = "${toString config.nps.defaultUid}:${toString config.nps.defaultGid}";
          volumeMap.config = "${storage}/${seerrName}/config:/app/config";
          environment.PORT = 5055;

          port = 5055;
          traefik.name = seerrName;
          stack = stackName;
          homepage = {
            inherit category;
            name = seerrDisplayName;
            settings = {
              description = seerrDescription;
              icon = "overseerr";
            };
          };
          glance = {
            inherit category;
            description = seerrDescription;
            name = seerrDisplayName;
            id = seerrName;
            icon = "di:overseerr";
          };
        };

        ${maintainerrName} = lib.mkIf cfg.maintainerr.enable {
          image = "ghcr.io/maintainerr/maintainerr:3.27.0";
          user = "${toString config.nps.defaultUid}:${toString config.nps.defaultGid}";
          volumeMap = {
            data = "${storage}/${maintainerrName}/data:/opt/data";
            media = "${mediaStorage}:/media";
          };

          port = 6246;
          traefik.name = maintainerrName;
          stack = stackName;
          homepage = {
            inherit category;
            name = maintainerrDisplayName;
            settings = {
              description = maintainerrDescription;
              icon = "maintainerr";
            };
          };
          glance = {
            inherit category;
            description = maintainerrDescription;
            name = maintainerrDisplayName;
            id = maintainerrName;
            icon = "di:maintainerr";
          };
        };

        ${profilarrName} = lib.mkIf cfg.profilarr.enable {
          image = "ghcr.io/dictionarry-hub/profilarr:2.2.0";
          volumeMap.config = "${storage}/${profilarrName}/config:/config";

          extraEnv =
            {
              PUID = config.nps.defaultUid;
              PGID = config.nps.defaultGid;
              ORIGIN = cfg.containers.${profilarrName}.traefik.serviceUrl;
            }
            // lib.optionalAttrs cfg.profilarr.enableParser {
              PARSER_HOST = profilarrParserName;
              PARSER_PORT = 5000;
            }
            // lib.optionalAttrs cfg.profilarr.oidc.enable {
              AUTH = "oidc";
              OIDC_DISCOVERY_URL = "${config.nps.containers.authelia.traefik.serviceUrl}/.well-known/openid-configuration";
              OIDC_CLIENT_ID = profilarrName;
              OIDC_CLIENT_SECRET.fromFile = cfg.profilarr.oidc.clientSecretFile;
            };

          wantsContainer = lib.optional cfg.profilarr.enableParser profilarrParserName;

          port = 6868;
          traefik.name = profilarrName;
          stack = stackName;
          homepage = {
            inherit category;
            name = profilarrDisplayName;
            settings = {
              description = profilarrDescription;
              icon = "profilarr";
            };
          };
          glance = {
            inherit category;
            description = profilarrDescription;
            name = profilarrDisplayName;
            id = profilarrName;
            icon = "di:profilarr";
          };
        };

        ${profilarrParserName} = lib.mkIf cfg.profilarr.enableParser {
          image = "ghcr.io/dictionarry-hub/profilarr-parser:2.2.0";
          stack = stackName;
          glance = {
            inherit category;
            name = "Profilarr Parser";
            parent = profilarrName;
            icon = "di:profilarr";
          };
        };

        ${sonarrName} = lib.mkIf cfg.sonarr.enable (arrlib.mkArrBase sonarrName
          // {
            image = "lscr.io/linuxserver/sonarr:4.0.19";
            port = 8989;

            homepage = {
              inherit category;
              name = sonarrDisplayName;
              settings = {
                description = sonarrDescription;
                icon = "sonarr";
                widget.type = "sonarr";
              };
            };
            glance = {
              inherit category;
              description = sonarrDescription;
              name = sonarrDisplayName;
              id = sonarrName;
              icon = "di:sonarr";
            };
          });

        ${radarrName} = lib.mkIf cfg.radarr.enable (arrlib.mkArrBase radarrName
          // {
            image = "lscr.io/linuxserver/radarr:6.3.0";
            port = 7878;

            homepage = {
              inherit category;
              name = radarrDisplayName;
              settings = {
                description = radarrDescription;
                icon = "radarr";
                widget.type = "radarr";
              };
            };
            glance = {
              inherit category;
              description = radarrDescription;
              name = radarrDisplayName;
              id = radarrName;
              icon = "di:radarr";
            };
          });

        ${bazarrName} = lib.mkIf cfg.bazarr.enable (arrlib.mkArrBase bazarrName
          // {
            image = "lscr.io/linuxserver/bazarr:1.6.0";
            port = 6767;

            homepage = {
              inherit category;
              name = bazarrDisplayName;
              settings = {
                description = bazarrDescription;
                icon = "bazarr";
                widget.type = "bazarr";
              };
            };
            glance = {
              inherit category;
              description = bazarrDescription;
              name = bazarrDisplayName;
              id = bazarrName;
              icon = "di:bazarr";
            };
          });
      }
      // arrlib.arrDbs [sonarrName radarrName bazarrName];
  };
}
